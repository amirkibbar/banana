plugins {
    id "com.jfrog.bintray" version "1.5"
    id 'org.ajoberstar.github-pages' version '1.3.2'
}

apply plugin: 'java'
apply plugin: 'maven-publish'
apply plugin: 'org.ajoberstar.github-pages'


group = "ajk.metrics.newrelic"
version = "0.0.3"
String repoName = "banana"

repositories {
    jcenter()
}

dependencies {
    compile "com.newrelic.agent.java:newrelic-api:3.23.0"
    compile "io.dropwizard.metrics:metrics-core:3.1.2"
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

def pomConfig = {
    licenses {
        license {
            name "The Apache Software License, Version 2.0"
            url "http://www.apache.org/licenses/LICENSE-2.0.txt"
            distribution "repo"
        }
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'a New Relic metrics reporter (see: http://metrics.dropwizard.io/3.1.0/, http://newrelic.com/)')
                root.children().last() + pomConfig
            }

            from components.java

            artifact sourcesJar {
                classifier "sources"
            }

            artifact javadocJar {
                classifier "javadoc"
            }
        }
    }
}

task install(dependsOn: 'publishMavenPublicationToMavenLocal') << {
    logger.lifecycle "installing $project.name version $version"
}

model {
    tasks.generatePomFileForMavenPublication {
        destination = file("$buildDir/libs/${project.name}-${project.version}.pom")
    }
}

bintray {
    user = System.getenv("BINTRAY_USER")
    key = System.getenv("BINTRAY_KEY")

    publications = ['maven']

    publish = true

    pkg {
        repo = 'maven'
        name = 'metrics-newrelic-reporter'
        websiteUrl = "https://github.com/amirkibbar/$repoName"
        issueTrackerUrl = "https://github.com/amirkibbar/$repoName/issues"
        vcsUrl = "https://github.com/amirkibbar/${repoName}.git"
        licenses = ['Apache-2.0']
        labels = ['metrics', 'new relic', repoName]
        version {
            name = project.version
        }
    }
}

githubPages {
    repoUri = "https://github.com/amirkibbar/banana.git"
    pages {
        from javadoc
    }
}

assemble.dependsOn = [sourcesJar, javadocJar]
bintrayUpload.dependsOn = [sourcesJar]

task publishAll(dependsOn: [bintrayUpload, publishGhPages]) << {}